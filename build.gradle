plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

group = 'com.tencent'
version = '1.0.0-SNAPSHOT'
description = 'Dataflow - Data Pipeline Orchestration Platform'

ext {
    set('springBootVersion', '3.5.8')
    set('mybatisStarterVersion', '3.0.3')
    set('colaComponentsVersion', '5.0.0')
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
        dependencies {
            dependency "com.alibaba.cola:cola-component-dto:${colaComponentsVersion}"
            dependency "com.alibaba.cola:cola-component-exception:${colaComponentsVersion}"
            dependency "com.alibaba.cola:cola-component-statemachine:${colaComponentsVersion}"
            dependency "org.mybatis.spring.boot:mybatis-spring-boot-starter:${mybatisStarterVersion}"
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'junit:junit:4.13.2'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
